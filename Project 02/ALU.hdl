// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
   // zx (set x to 0)
   Mux16(a=x, b=false, sel=zx, out=zxMuxOut);
   // nx (invert x)
   Not16(in=zxMuxOut, out=notzxMuxOut);
   Mux16(a=zxMuxOut, b=notzxMuxOut, sel=nx, out=nxMuxOut);
   // zy (set y to 0)
   Mux16(a=y, b=false, sel=zy, out=zyMuxOut);
   // ny (invert y)
   Not16(in=zyMuxOut, out=notzyMuxOut);
   Mux16(a=zyMuxOut, b=notzyMuxOut, sel=ny, out=nyMuxOut);
   // f (Add if 1, otherwise bitwise AND)
   Add16(a=nxMuxOut, b=nyMuxOut, out=add16Out);
   And16(a=nxMuxOut, b=nyMuxOut, out=and16Out);
   Mux16(a=and16Out, b=add16Out, sel=f, out=fAndorAddOut);
   // no (invert f, prep inputs for zr and ng gets most significant bit)
   Not16(in=fAndorAddOut, out=notFOut);
   Mux16(a=fAndorAddOut, b=notFOut, sel=no, out=out, out[0..7]=zr0thru7, out[8..15]=zr8thru15, out[15]=ng);
   // zr ()
   Or8Way(in=zr0thru7, out=or0thru7Out);
   Or8Way(in=zr8thru15, out=or8thru15Out);
   Or(a=or0thru7Out, b=or8thru15Out, out=orOut);
   Not(in=orOut, out=zr);
}